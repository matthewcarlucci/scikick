# This Snakefile must be run from the same directory as 
# the project's scikick.yml. 

###########################################
# Imports
###########################################

from scikick.config import ScikickConfig 
from scikick.utils import warn, get_sk_exe_dir, get_sk_snakefile
import tempfile

# For debugging outside snakemake context
import os
import sys
from snakemake import *

###########################################
# Workflow variables
###########################################

# trying to eventually remove this
# configfile: 'scikick.yml'
# Instead of snakemake config file,
# import the scikick.yml config file directly.
skconfig = ScikickConfig()

# Getting properties
report_dir = skconfig.report_dir # directories
data_parent = "output"

# scikick system files
exe_dir = get_sk_exe_dir()
script_dir = os.path.join(exe_dir, "scripts")
template_dir = os.path.join(exe_dir, "template")
snake_src = get_sk_snakefile()
generate_html_exe = os.path.join(script_dir, "render.R")

inner_ymls = [os.path.join(skconfig.report_dir, "out_md", dir, "_site.yml")
	for dir in set([os.path.dirname(a) for a in skconfig.analysis.keys()])]
###########################################
# Converting scikick config to I/O deps
# for the snakemake rules
###########################################
rmd_dict = {}
for key in skconfig.analysis:
	rmd_dict[os.path.splitext(key)[0]] = key

rmd_deps = skconfig.inferred_deps

# add a template index to the workflow if index doesn't exist
sys_index = os.path.join(template_dir, 'index.Rmd')
index_list = list(filter(lambda f: os.path.basename(f) == "index", \
	rmd_dict.keys()))

# Use the template as homepage if there are multiple or no index files
if len(index_list) != 1:
	# Verbose mode only?
	#warn("sk: Include an index.Rmd to add content " +\
	#	"to the required index.html page")
	rmd_dict['index'] = rmd_deps['index'] = sys_index
else:
	rmd_dict['index'] = rmd_dict.pop(os.path.splitext(index_list[0])[0], None)
	rmd_deps['index'] = rmd_deps.pop(os.path.splitext(index_list[0])[0], None)
index_rmd = rmd_dict["index"]

# capture the name of the log file
warn(f"SK INTERNAL: logfile {logger.logfile}")

###########################################
# Workflow rules
# - messages are used by status.py
###########################################

# Rules that should never require cluster execution
localrules: generate_site_files, generate_html, sk_done

rule sk_done:
	input:
		expand('{report_dir}/out_html/{report_step}.html',
			report_step = list(rmd_dict.keys()), report_dir = skconfig.report_dir)
	message: "All rules from {snake_src} are complete"

# Currently handles all code execution (Rmd and R to md) 
rule execute_code:
	input:
		deps = lambda wildcards: rmd_deps[wildcards.report_step],
		rmd = lambda wildcards: rmd_dict[wildcards.report_step]
	output:
		md = os.path.join(skconfig.report_dir, "out_md", "{report_step}.md")
	message: "Executing R code in {input.rmd}, " + \
			"outputting to {output.md}"
	params:
		simplified_input = len("{input.rmd}"),
		data_parent = data_parent,
		template_dir = template_dir,
		script_dir = script_dir
	conda: skconfig.snakefile_arg("conda")
	singularity: skconfig.snakefile_arg("singularity")
	threads: skconfig.snakefile_arg("threads")
	benchmark: skconfig.snakefile_arg("benchmark") + "{report_step}" if skconfig.snakefile_arg("benchmark") != "" else ""
	# 'script:' section causes directories to not get found when using singularity, so 'shell:' is used
	shell: "Rscript %s {input.rmd} {output.md} {params.template_dir} \
		{params.data_parent} {params.script_dir} {logger.logfile} \
		{index_rmd}" \
		% os.path.join(script_dir, "execute_code.R")

# Generate all _site.yml files
rule generate_site_files:
	input: yaml = "scikick.yml"
	output:
		main_yml = os.path.join(skconfig.report_dir, "out_md", "_site.yml"),
		inner_ymls = inner_ymls
	message: "Creating site layout from scikick.yml"
	script: os.path.join(script_dir, 'yamlgen.py')

# md => HTML via rmarkdown::render
rule generate_html:
	input:
		yaml = "{report_dir}/out_md/_site.yml",
		md = "{report_dir}/out_md/{report_step}.md"
	output:
		html = "{report_dir}/out_html/{report_step}.html"
	message: "Converting {input.md} to {output.html}"
	params:
		template_dir = template_dir,
		index_html = lambda w: os.path.join(os.path.relpath(
			os.path.join(report_dir, "out_md"),
			os.path.dirname(os.path.join(report_dir,
				"out_md", "%s.md" % w.report_step))), "index.html")
	conda: skconfig.snakefile_arg("conda")
	singularity: skconfig.snakefile_arg("singularity")
	# using 'shell:' instead of 'script:' for compatibility with renv
	shell: "Rscript {generate_html_exe} {input.md} {output} {params.template_dir} {params.index_html}" 


# Include the project's Snakefile if it exists
user_snakefile = os.path.join(os.getcwd(), "Snakefile")
if os.path.isfile(user_snakefile):
	warn("sk: Including Snakefile found in project directory")
	include: user_snakefile
